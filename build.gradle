plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id "org.sonarqube" version "3.4.0.2513"
}

group = 'com.az.farecard'
version = '0.0.1'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'
	// https://mvnrepository.com/artifact/com.auth0/java-jwt
	implementation group: 'com.auth0', name: 'java-jwt', version: '4.0.0'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
//	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
}


test {
	finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

jacocoTestReport {
	reports {
		xml.enabled(true)
	}
}
jacocoTestReport {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude:  [
					'com/az/farecard/dto/**',
					'com/az/farecard/entity/**',
					'com/az/farecard/exception/**',
					'com/az/farecard/repository/**',
					'com/az/farecard/AzFareCardApplication.class'
			])
		}))
	}
}
sonarqube {
	properties {
		property 'sonar.projectName', 'az-farecard-service'
		property 'sonar.projectKey', 'az-farecard-service'
		property 'sonar.host.url', 'http://192.168.1.21:9000'
		property "sonar.exclusions", "**/com/az/farecard/dto/**, **/com/az/farecard/entity/**, **/com/az/farecard/exception/**,	**/com/az/farecard/repository/**, **/com/az/farecard/AzFareCardApplication.java"
		property "sonar.issue.ignore.multicriteria", "e1"
		property "sonar.issue.ignore.multicriteria.e1.resourceKey", "src/test/java/**/*"
		property "sonar.issue.ignore.multicriteria.e1.ruleKey", "*"
		//property 'sonar.login', 'sqp_eca0ff40a200f3f41e9eb1a176a8a050389f069d'
	}
}

tasks.named('sonarqube').configure {
	dependsOn test
}

sourceSets {
	main {
		java {
			srcDirs "src/main/java"
		}
	}

	test {
		java {
			srcDirs "src/test/java"
		}
		resources {
			srcDir file('src/test/java')
			exclude '**/*.java'
		}
	}
}